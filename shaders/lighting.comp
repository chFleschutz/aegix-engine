#version 450

layout(local_size_x = 16, local_size_y = 16) in;

layout(set = 0, binding = 0, rgba16) uniform image2D sceneColorMap;
layout(set = 0, binding = 1, rgba16) uniform image2D positionMap;
layout(set = 0, binding = 2, rgba16) uniform image2D normalMap;
layout(set = 0, binding = 3, rgba8) uniform image2D albedoMap;
layout(set = 0, binding = 4, rgba8) uniform image2D armMap;
layout(set = 0, binding = 5, rgba8) uniform image2D emissiveMap;

struct AmbientLight
{
	vec4 color;
};

struct DirectionalLight
{
	vec4 direction;
	vec4 color;
};

struct PointLight
{
	vec4 position;
	vec4 color;
};

layout(set = 0, binding = 6) uniform Lighting
{
	vec4 cameraPosition;
	AmbientLight ambientLight;
	DirectionalLight directionalLight;
	PointLight pointLights[32];
	int numPointLights;
} lighting;

const float PI = 3.14159265359;

// Settings
const float EMISSIVE_FACTOR = 10.0;

float lightAttenuation(vec3 lightPos, vec3 fragPos);
float NormalDistributionGGX(vec3 N, vec3 H, float roughness);
float GeometrySchlickGGX(float NdotV, float roughness);
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness);
vec3 FresnelSchlick(float cosTheta, vec3 F0);
vec3 PBRLighting(vec3 N, vec3 V, vec3 L, vec3 albedo, float roughness, float metallic, vec3 radiance, vec3 F0);

void main()
{
	ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);

	vec3 position = imageLoad(positionMap, pixelCoords).rgb;
    vec3 normal = imageLoad(normalMap, pixelCoords).rgb;
	vec3 albedo = imageLoad(albedoMap, pixelCoords).rgb;
	vec3 arm = imageLoad(armMap, pixelCoords).rgb;
	vec3 emissive = imageLoad(emissiveMap, pixelCoords).rgb;

    float ambientOcclusion = arm.r;
    float roughness = arm.g;
    float metallic = arm.b;

	vec3 N = normalize(normal);
	vec3 V = normalize(lighting.cameraPosition.xyz - position);
	
    // Tint reflection for metals
    vec3 F0 = vec3(0.04); // default for dielectric materials
    F0 = mix(F0, albedo, metallic);

    vec3 Lo = vec3(0.0);
    for (int i = 0; i < lighting.numPointLights; i++)
    {
        PointLight pointLight = lighting.pointLights[i];
        vec3 L = normalize(pointLight.position.xyz - position);

        float attenuation = lightAttenuation(pointLight.position.xyz, position);
        vec3 radiance = pointLight.color.rgb * pointLight.color.w * attenuation;

        Lo += PBRLighting(N, V, L, albedo, roughness, metallic, radiance, F0);
    }

    // Ambient light
    vec3 ambientLight = lighting.ambientLight.color.rgb * lighting.ambientLight.color.w;
    Lo += albedo * ambientLight;

    // Directional light
    vec3 L = normalize(-lighting.directionalLight.direction.xyz);
    vec3 radiance = lighting.directionalLight.color.rgb * lighting.directionalLight.color.w;
    Lo += PBRLighting(N, V, L, albedo, roughness, metallic, radiance, F0);

    // Emissive material
    Lo += emissive * EMISSIVE_FACTOR;

    imageStore(sceneColorMap, pixelCoords, vec4(Lo, 1.0));
}

float lightAttenuation(vec3 lightPos, vec3 fragPos)
{
	float d = length(lightPos - fragPos);
    return 1.0 / (d * d);
}

float NormalDistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness * roughness;
    float a2 = a * a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH * NdotH;

    float denum = (NdotH2 * (a2 - 1.0) + 1.0);
    return a2 / (PI * denum * denum);
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r * r) / 8.0;

    float denum = NdotV * (1.0 - k) + k;
    return NdotV / denum;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx1 = GeometrySchlickGGX(NdotV, roughness);
    float ggx2 = GeometrySchlickGGX(NdotL, roughness);
    return ggx1 * ggx2;
}

vec3 FresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

vec3 PBRLighting(vec3 N, vec3 V, vec3 L, vec3 albedo, float roughness, float metallic, vec3 radiance, vec3 F0)
{
    vec3 H = normalize(V + L);

    // Cook-Torrance BRDF
    float D = NormalDistributionGGX(N, H, roughness);
    float G = GeometrySmith(N, V, L, roughness);
    vec3 F = FresnelSchlick(max(dot(H, V), 0.0), F0);
    vec3 kS = F;
    vec3 kD = (1.0 - kS) * (1.0 - metallic);

    vec3 DFG = D * G * F ;
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    vec3 specular = DFG / (4.0 * NdotV * NdotL + 0.0001);

    return (kD * albedo / PI + specular) * radiance * NdotL;
}
