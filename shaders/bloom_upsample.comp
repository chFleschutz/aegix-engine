#version 450

layout(local_size_x = 16, local_size_y = 16) in;

layout(set = 0, binding = 0, rgba16f) uniform writeonly image2D outBloomMap;
layout(set = 0, binding = 1) uniform sampler2D bloomMipMap;

const float filterScale = 1.0;

void main()
{
	ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);
	vec2 texelSize = 1.0 / vec2(imageSize(outBloomMap));
	vec2 uv = (vec2(pixelCoords) + 0.5) * texelSize;

	float xStep = texelSize.x * filterScale;
	float yStep = texelSize.y * filterScale;

	// Take 9 samples around the current texel (e)
	// a - b - c
	// d - e - f
	// g - h - i
	vec3 a = texture(bloomMipMap, vec2(uv.x - xStep, uv.y - yStep)).rgb;
	vec3 b = texture(bloomMipMap, vec2(uv.x,         uv.y - yStep)).rgb;
	vec3 c = texture(bloomMipMap, vec2(uv.x + xStep, uv.y - yStep)).rgb;
	vec3 d = texture(bloomMipMap, vec2(uv.x - xStep, uv.y)).rgb;
	vec3 e = texture(bloomMipMap, vec2(uv.x,         uv.y)).rgb;
	vec3 f = texture(bloomMipMap, vec2(uv.x + xStep, uv.y)).rgb;
	vec3 g = texture(bloomMipMap, vec2(uv.x - xStep, uv.y + yStep)).rgb;
	vec3 h = texture(bloomMipMap, vec2(uv.x,         uv.y + yStep)).rgb;
	vec3 i = texture(bloomMipMap, vec2(uv.x + xStep, uv.y + yStep)).rgb;

	// Apply 3x3 gaussian filter weights
	//        | 1 2 1 |
	// 1/16 * | 2 4 2 | 
	//        | 1 2 1 |
	vec3 bloomColor = (a + c + g + i) * 0.0625 + (b + d + f + h) * 0.125 + e * 0.25;
	imageStore(outBloomMap, pixelCoords, vec4(bloomColor, 1.0));
}